# This policy validates that all modules loaded directly by the
# root module are in the Private Module Registry (PMR) of a TFC
# organization.

# Import the v2 tfplan import, but use the alias "tfplan"
import "tfconfig/v2" as tfconfig

# Standard strings import
import "strings"

##### Parameters #####
# The address of the TFC or TFE server
param address default "app.terraform.io"

# The organization on the TFC or TFE server
param organization

# Require all modules directly under root module to come from PMR
require_modules_from_pmr = func(tf_address, tf_org) {

  validated = true

  # Iterate over all modules but focus on calls from the root module.
  # A module call represents one module referencing another module
  # with a module block.
  for tfconfig.module_calls as index, mc {
    # Ignore modules with index matching "<address>:<name>"
    # since these are not called from the root module.
    # Since <address> would start with "module." for a non-root module,
    # we could look for indices that do not match "^module\\.(.+):(.+)"
    if index not matches "(.+):(.+)" {
    # The following line could be used instead of the previous line
    # if index matches "^[^:]*$" {
      if not strings.has_prefix(mc.source, tf_address + "/" + tf_org) {
        print("Your root module called the module", index, "with source", mc.source)
        validated = false
      } // end second if
    } // end first if
  } // end for

  if not validated {
    print("All modules called from the root module must come from the",
          "private module registry", tf_address + "/" + tf_org)
  }

  return validated
}

# Call the validation function
modules_from_pmr = require_modules_from_pmr(address, organization)

# Main rule
main = rule {
  modules_from_pmr
}
